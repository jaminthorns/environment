#!/usr/bin/env fish

set paths (non_option_args $argv)
set escaped_paths (string escape -- $paths)

set options (option_args $argv)
set escaped_options (string escape -- $options)

set header (git show --format= --shortstat $options -- $paths | string trim)
set revision (git show --format=%H --no-patch $options)

if test (count $header) -eq 0
    set header "No changes"
end

set -a DELTA_FEATURES navigate-changes

function list_function -V options -V paths
    git show --format= --name-status $options -- $paths | format_git_name_status
end

source (status dirname)/git_interactive_command.fish \
    --fzf-flag=--ansi \
    --fzf-flag=--multi \
    --fzf-flag=--scheme=path \
    --fzf-flag=--header=$header \
    --main-view-name=changes \
    --items-variable=files \
    --no-items-message="No file selected." \
    --hyperlink-format="vscode://jaminthorns.gitterm/$revision:{path}:{line}" \
    --list-function=list_function \
    --items-command="cut -d \$SEP -f 2,4 | string split \$SEP" \
    --view-command="git -C (git rev-parse --show-toplevel) show --format= $escaped_options -- \$items" \
    --summary-command="git show --color --decorate --no-patch $escaped_options -- $escaped_paths"

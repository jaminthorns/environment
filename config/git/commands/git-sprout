#!/usr/bin/env fish

if not test (count $argv) -eq 2
    echo "fatal: must supply base revision and branch name" >&2
    exit 1
end

set base_revision $argv[1]
set branch_name $argv[2]
set current_branch (git branch --show-current)

if git rev-parse --verify $branch_name >/dev/null 2>&1
    echo "fatal: branch $branch_name already exists" >&2
    exit 1
end

git switch -c $branch_name
git commit

if not test -z (git status --porcelain | string collect)
    set should_stash
end

if set -q should_stash
    git stash --include-untracked
end

git rebase --onto $base_revision HEAD~
git switch $current_branch

if set -q should_stash
    git stash pop --index
end

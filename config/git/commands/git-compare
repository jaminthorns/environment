#!/usr/bin/env fish

argparse --ignore-unknown "p/path=+" -- $argv

set args (string escape -- $argv)
set paths -- (string escape $_flag_path)
set header (git diff --shortstat $argv -- $_flag_path)

if test (count $header) -eq 0
    set header "No changes"
end

set first_arg (string match -e -v -r "^-" -- $args)

function dots_regex -a count
    echo "(?<!\.)\.{$count}(?!\.)"
end

if string match -q -r (dots_regex 2) $first_arg
    set commit_range (string replace -r (dots_regex 2) "..." $first_arg)
    set summary_command "git log --graph --format=comfortable-source --color $commit_range $paths"
else if string match -q -r (dots_regex 3) $first_arg
    set commit_range (string replace -r (dots_regex 3) ".." $first_arg)
    set summary_command "git log --format=comfortable --color $commit_range $paths"
else
    set highlight (set_color cyan)
    set reset (set_color reset)
    set summary_command "echo 'Could not determine a commit range (use $highlight<commit>..<commit>$reset or $highlight<commit>...<commit>$reset syntax).' | less"
end

source (status dirname)/git_fzf_command.fish \
    --flags "--ansi --multi" \
    --header $header \
    --main-view-name changes \
    --items-variable files \
    --no-items-message "No file selected." \
    --list-command "git diff --name-status $args $paths | format_git_name_status" \
    --items-command "cut -d \$separator -f 2,4 | string split \$separator" \
    --view-command "git -C (git rev-parse --show-toplevel) diff $args -- \\\$items" \
    --summary-command $summary_command

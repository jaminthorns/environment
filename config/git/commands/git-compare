#!/usr/bin/env fish

argparse --ignore-unknown "p/path=+" -- $argv

set args (string escape -- $argv)

if set -q _flag_path
    set paths -- $_flag_path
else
    set paths --
end

set header (git diff --shortstat $argv $paths)

if test (count $header) -eq 0
    set header "No changes"
end

set first_arg (string match -e -v -r "^-" -- $args)

function dots_regex -a count
    echo "(?<!\.)\.{$count}(?!\.)"
end

if string match -q -r (dots_regex 2) $first_arg
    set commit_range (string replace -r (dots_regex 2) "..." $first_arg)
    set summary_command "git graph $commit_range $paths"
else if string match -q -r (dots_regex 3) $first_arg
    set commit_range (string replace -r (dots_regex 3) ".." $first_arg)
    set summary_command "git graph $commit_range $paths"
else
    set summary_command "echo Could not determine a commit range. | less"
end

source (status dirname)/git_fzf_command.fish \
    --flags "--ansi --multi" \
    --header $header \
    --items-variable files \
    --list-command "git diff --name-status $args $paths | format_git_name_status" \
    --item-command "cut -d ' ' -f 2,4 | string split ' '" \
    --view-command "git -C (git rev-parse --show-toplevel) diff $args -- \\\$item" \
    --summary-command $summary_command

#!/usr/bin/env fish

argparse --ignore-unknown "p/path=+" -- $argv

set args (string escape -- $argv)
set paths -- (string escape $_flag_path)
set header (git show --format= --shortstat $argv -- $_flag_path)

if test (count $header) -eq 0
    set header "No changes"
end

set -a DELTA_FEATURES navigate-changes

if set -q GITTERM_REVISION
    set hyperlink_format_flag --hyperlink-format "vscode://jaminthorns.gitterm/$GITTERM_REVISION:{path}:{line}"
end

source (status dirname)/git_fzf_command.fish \
    --fzf-flags "--ansi --multi --scheme=path" \
    --header $header \
    --main-view-name changes \
    --items-variable files \
    --no-items-message "No file selected." \
    --list-command "git show --format= --name-status $args $paths | format_git_name_status" \
    --items-command "cut -d \$SEP -f 2,4 | string split \$SEP" \
    --view-command "git -C (git rev-parse --show-toplevel) show --format="" $args -- \\\$items" \
    --summary-command "git show --color --decorate --no-patch $args $paths" \
    $hyperlink_format_flag

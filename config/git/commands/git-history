#!/usr/bin/env fish

set paths (non_option_args $argv)
set escaped_paths (string escape -- $paths)

argparse --ignore-unknown graph source follow "s/show-options=" -- (option_args $argv)
set options $argv

if set -q _flag_follow
    set -x commit_filenames (string replace -a -r "\n\n" "\t" (git log $options --follow --name-status --format="%H" -- $paths | string collect || exit 1) | string collect)

    if test (count $commit_filenames) -eq 0
        echo "fatal: no file history for '$paths'" >&2
        exit 1
    else
        set escaped_paths "(echo \"\$commit_filenames\" | grep \$items | cut -f 3- | string split \t)"
    end
end

if set -q GITTERM_SEARCH
    set pager_flags_flag --pager-flags="--jump-target=.5 --pattern=$(string escape $GITTERM_SEARCH)"
end

set list_toggles (create_toggles)
set -q _flag_graph && toggle $list_toggles graph
set -q _flag_source && toggle $list_toggles source

function list_function -V list_toggles -V _flag_follow -V options -V paths
    if enabled $list_toggles graph
        set -a options --graph
    else if set -q _flag_follow
        set -a options --follow
    end

    if enabled $list_toggles source
        set -a options --format=comfy-source
    else
        set -a options --format=comfy
    end

    git log -z --color $options -- $paths
end

set list_command "$(functions --no-details list_function); list_function"

set -a DELTA_FEATURES navigate-files

source (status dirname)/git_fzf_command.fish \
    --fzf-flag=--read0 \
    --fzf-flag=--ansi \
    --fzf-flag=--multi \
    --fzf-flag=--track \
    --fzf-flag=--scheme=history \
    --fzf-flag="--bind=alt-g:execute-silent(toggle $list_toggles graph)+reload($list_command)" \
    --fzf-flag="--bind=alt-s:execute-silent(toggle $list_toggles source)+reload($list_command)" \
    --main-view-name=history \
    --items-variable=commits \
    --no-items-message="No commit selected." \
    --hyperlink-format="vscode://jaminthorns.gitterm/\$items:{path}:{line}" \
    --list-function=list_function \
    --items-command="awk 'NR % 2 == 1' | sed -nE 's/[^0-9a-f]*([0-9a-f]+).*/\1/p' | xargs git rev-parse" \
    --view-command="git show --color --decorate --diff-merges=on $_flag_show_options \$items -- $escaped_paths" \
    $pager_flags_flag

delete_toggles $list_toggles

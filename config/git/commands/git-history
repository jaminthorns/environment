#!/usr/bin/env fish

argparse --ignore-unknown "p/path=+" "f/file=" "o/show-options=" -- $argv

set args (string escape -- $argv)

if set -q _flag_path
    set paths -- $_flag_path
else if set -q _flag_file
    set file_names (git log --format="" --follow --name-only -- $_flag_file | uniq)

    if test (count $file_names) -eq 0
        echo "fatal: no file history for '$_flag_file'"
        exit 1
    else
        set paths -- $file_names
    end
else
    set paths --
end

source (status dirname)/git_fzf_command.fish \
    --flags "--ansi --multi --track --scheme=history" \
    --items-variable commits \
    --list-command "git graph --color $args $paths" \
    --item-command "sed -nE 's/[^0-9a-f]*([0-9a-f]+).*/\1/p' | xargs git rev-parse" \
    --view-command "xargs -I [] git show --color $_flag_show_options [] $paths"
